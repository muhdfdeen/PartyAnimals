plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = 'com.muhdfdeen'
version = '0.4.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    implementation("de.exlll:configlib-paper:4.8.0")
    implementation("org.bstats:bstats-bukkit:3.1.0")
    compileOnly("me.clip:placeholderapi:2.11.7")
    implementation("com.github.NuVotifier:NuVotifier:2.7.2")
    implementation("org.xerial:sqlite-jdbc:3.51.1.0")
}

tasks.named('shadowJar') {
  relocate 'de.exlll.configlib', 'com.muhdfdeen.partyanimals.libs.configlib'
  relocate 'org.bstats', 'com.muhdfdeen.partyanimals.libs.bstats'
  exclude 'plugin.yml'
  minimize()
  archiveClassifier.set('')
}

build.dependsOn shadowJar

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
